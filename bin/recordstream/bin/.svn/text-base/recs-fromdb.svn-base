#!/usr/bin/perl

$| = 1;

use strict;
use warnings;

use DBI;
use Getopt::Long;
use Data::Dumper;

use Recs::DBHandle;
use Recs::OutputStream;
use Recs::Record;

my ($table_name, $sql);

Getopt::Long::Configure("pass_through");
GetOptions(
   'table=s' => \$table_name,
   'sql=s'   => \$sql,
);

my $dbh = Recs::DBHandle::get_dbh();

unless ( $table_name || $sql ) {
   usage("Must define --table or --sql \n");
}

unless ( $sql ) {
   $sql = "SELECT * FROM $table_name";
}

my $out = Recs::OutputStream->new();

my $sth = $dbh->prepare($sql);
$sth->execute();

while ( my $row = $sth->fetchrow_hashref() ) {
   my $record = Recs::Record->new(%$row);
   $out->put_record($record);
}

sub usage {
   my $message = shift;

   print <<USAGE;
$0
$message
   Recs from DB will execute a select statement on a database of your choice,
   and create a record stream from the results.  The keys of the record will be
   the column names and the values the row values.

   --table - Name of the table to dump, this is a shortcut for 
             --sql 'SELECT * from tableName'
   --sql   - SQL select statement to run

USAGE

   Recs::DBHandle::usage();

   print <<EXAMPLES;
Examples:
   # Dump a table
   $0 --type sqlite --dbfile testDb --table recs

   # Run a select statement
   $0 --dbfile testDb --sql 'SELECT * FROM recs WHERE id > 9'

EXAMPLES

   exit 1;
}
